__scope = function()
globals.MiniMock = {}
MiniMock.Any = {}
isGH = globals.hasIndex("isGH") // use colours if set.
ifGH = function(s)
	if isGH then return s
	return ""
end function
ERR    = ifGH("<color=#ff0000>")
ERR_E  = ifGH("</color>")
DATA   = ifGH("<color=#bfb360>")
DATA_E = ifGH("</color>")
BARG   = ifGH("<color=#ff4203>")
BARG_E = ifGH("</color>")
FUNC   = ifGH("<b><color=#3644a3>")
FUNC_E = ifGH("</color></b>")
SITE   = ifGH("<color=#452166>")
SITE_E = ifGH("</color>")
LF = char(10)
stacktrace = function(); return ""; end function
if globals.hasIndex("__Intrinsics") and globals.__Intrinsics.hasIndex("stackTrace") then stacktrace = function()
	return LF + globals.__Intrinsics.stackTrace[2:].join(LF)
end function
if globals.hasIndex("IS_GREYBEL") then stacktrace = function()
    return LF + include_lib("/lib/testlib.so").get_stack_trace[2:].join(LF)
end function
MiniMock.__mockCall = function(actualParams, funcName)
	if self._expect.len == 0 or funcName != self._expect[0].target then
		s = ERR + "Fatal error, unexpected call to " + FUNC + funcName + FUNC_E + ":" + ERR_E +
		    LF + "      params: " + DATA + actualParams + DATA_E
		for expect in self._expect
			s = s + LF + "      expecting: " + FUNC + expect.target + FUNC_E +
			        LF + "         params: " + DATA + expect.params + DATA_E
			if expect.expectedAt != "" then s = s + LF + "  registered at: " + SITE + expect.expectedAt + SITE_E
		end for
		if self.__builtAt then s = s + LF + "  created at: " + SITE + self.__builtAt + SITE_E
		s = s + stacktrace
		if MINITEST_RUNNING then minitest_print_error(s, true)
		print s
		exit()
	end if
	expected = self._expect.pull
	expectedArgCnt = self._argCnt[expected.target]
	if expectedArgCnt != actualParams.len then
		s = ERR + "Fatal error, wrong argument count:" + ERR_E
		if actualParams.len > 0 then s = s +
		    LF + "      params: " + DATA + actualParams + DATA_E
		s = s + LF + "    expected: " + DATA + expectedArgCnt + DATA_E + " args" +
		        LF + "      actual: " + DATA + actualParams.len + DATA_E + " args" +
			LF + "expecting function: " + FUNC + expected.target + FUNC_E
		if expected.expectedAt != "" then s = s + LF + "     registered at: " + SITE + expected.expectedAt + SITE_E
		s = s + stacktrace
		if MINITEST_RUNNING then minitest_print_error(s, true)
		print s
		exit()
	end if
	for i in expected.params.indexes
		if not expected.params[i] == MiniMock.Any and expected.params[i] != actualParams[i] then
			s = "Error In expected call to " + FUNC + expected.target + FUNC_E + LF
			s = s + "    expected args: " + DATA + expected.params + DATA_E + LF
			s = s + "      actual args: " + BARG + actualParams + BARG_E
			if expected.expectedAt != "" then s = s + LF + "    registered at: " + SITE + expected.expectedAt + SITE_E
			s = s + stacktrace
			p = @print
			if MINITEST_RUNNING then p = @minitest_print_error
			p s
			break
		end if
	end for
	maybeRet = null
	if @expected.clbk then maybeRet = expected.clbk(actualParams)
	if @expected.retVal == null then return maybeRet
	return @expected.retVal
end function

MiniMock.genFunc = function(paramCount, funcName)
	this = self
	locals.funcName = @funcName
	funcs = []
	f = function()
		return this.__mockCall([], outer.funcName)
	end function
	funcs.push @f
	f = function(a)
		return this.__mockCall([@a], outer.funcName)
	end function
	funcs.push @f
	f = function(a, b)
		return this.__mockCall([@a, @b], outer.funcName)
	end function
	funcs.push @f
	f = function(a, b, c)
		return this.__mockCall([@a, @b, @c], outer.funcName)
	end function
	funcs.push @f
	f = function(a, b, c, d)
		return this.__mockCall([@a, @b, @c, @d], outer.funcName)
	end function
	funcs.push @f
	f = function(a, b, c, d, e)
		return this.__mockCall([@a, @b, @c, @d, @e], outer.funcName)
	end function
	funcs.push @f
	f = function(a, b, c, d, e, f)
		return this.__mockCall([@a, @b, @c, @d, @e, @f], outer.funcName)
	end function
	funcs.push @f
	f = function(a, b, c, d, e, f, g)
		return this.__mockCall([@a, @b, @c, @d, @e, @f, @g], outer.funcName)
	end function
	funcs.push @f
	f = function(a, b, c, d, e, f, g, h)
		return this.__mockCall([@a, @b, @c, @d, @e, @f, @g, @h], outer.funcName)
	end function
	funcs.push @f
	if funcs.len <= paramCount then exit("MiniMock: Fatal Error: No wrapper defined for " + paramCount + " params")
	// Add more as needed.
	return @funcs[paramCount]
end function

MiniMock._addMockFunc = function(kv)
	funcSig = str(@kv.value)
	//handle pathological case of default arg containing a comma
	while funcSig.indexOf("""") != null
		startQuote = funcSig.indexOf("""")
		endQuote = funcSig.indexOf("""", startQuote + 1)
		funcSig = funcSig[0:startQuote] + funcSig[endQuote+1:]
	end while
	paramCount = 1
	if funcSig.indexOf("FUNCTION()") == 0 then
		paramCount = 0
	else if funcSig.indexOf(",") != null then
		paramCount = funcSig.split(",").len
	end if
	self[@kv.key] = @self.genFunc(paramCount, @kv.key)
	self._argCnt[@kv.key] = paramCount
end function

MiniMock._addMockData = function(kv)
	self[kv["key"]] = @self.genFunc(0)
end function

MiniMock._build = function(mapToMock, ret)
	for kv in mapToMock
		if @kv.key == "__isa" then
			self._build(kv.value, ret)
		else if @kv.value isa funcRef then
			ret._addMockFunc(kv)
		else
			ret._addMockData(kv)
		end if
	end for
	return ret
end function

MiniMock.define = function(self, key, paramCount)
	func = @self.genFunc(paramCount, @key)
	self._argCnt[key] = paramCount
	self[key] = @func
	return @func
end function

MiniMock.build = function(mapToMock)
	map = {"__isa": mapToMock, "_expect": [], "_argCnt": {}}
	map.__builtAt = stacktrace.split(LF)[2]
	for kv in self
		map[@kv.key] = @kv.value
	end for
	return self._build(mapToMock, map)
end function

Expect = {}

Expect.build = function(funcName, argCnt, mock)
	ret = new Expect
	ret.target = funcName
	ret.params = []
	ret.argCnt = argCnt
	ret.clbk = null
	ret.retVal = null
	ret.mock = mock
	ret.expectedAt = stacktrace.split(LF)[3]
	return ret
end function

// params should be enclosed in a list[]
// However, a single argument will be converted for you.
Expect.withParams = function(params)
	if not params isa list then params = [params]
	if params.len != self.argCnt then
		s = ERR + "FATAL ERROR: Your test is passing the wrong arg quantity" + ERR_E +
		LF + "    function: " + FUNC + self.target + FUNC_E +
		LF + "    expected: " + DATA + self.argCnt + DATA_E + " args" +
		LF + "      actual: " + DATA + params.len + DATA_E + " args"
		if self.mock.__builtAt then s = s + LF + "  created at: " + SITE + self.mock.__builtAt + SITE_E
		s = s + stacktrace
		if MINITEST_RUNNING then minitest_print_error(s, true)
		print s
		exit()
	end if
	self.params = params
	return self
end function

Expect.thenInvoke = function(func)
	self.clbk = @func
	return self
end function

Expect.andReturn = function(retVal)
	self.retVal = @retVal
	return self
end function

MiniMock.expectCall = function(self, funcName)
	if not self._argCnt.hasIndex(funcName) then
		s = ERR + "FATAL ERROR: Your test is trying to expect a call to a non-existent function" + ERR_E +
		LF + "    function: " + FUNC + funcName + FUNC_E
		if self.__builtAt then s = s + LF + "  created at: " + SITE + self.__builtAt + SITE_E
		s = s + stacktrace
		if MINITEST_RUNNING then minitest_print_error(s, true)
		print s
		exit()
	end if
	ret = Expect.build(funcName, self._argCnt[funcName], self)
	self._expect.push(ret)
	return ret
end function
MiniMock.expectData = @MiniMock.expectCall

MiniMock.getResult = function(self)
	if self._expect.len == 0 then return true
	ret = "Error: Unfulfilled call(s):"
	for call in self._expect
		ret = ret + LF + "       callee: " + FUNC + call.target + FUNC_E
		if call.params.len > 0 then ret = ret + LF + "       params: " + DATA + call.params + DATA_E
		if call.expectedAt != "" then ret = ret + LF + "registered at: " + SITE + call.expectedAt + SITE_E
	end for
	if self.__builtAt then ret = ret + LF + "  created at: " + SITE + self.__builtAt + SITE_E
	ret = ret + stacktrace
	return ret
end function

end function //end __scope
if not globals.hasIndex("MiniMock") then __scope()
__scope = null
